apply plugin: rootProject.ext.plugins.application

android {
    compileSdkVersion rootProject.ext.androids.compileSdkVersion
    buildToolsVersion rootProject.ext.androids.buildToolsVersion

    defaultConfig {
        applicationId rootProject.ext.androids.applicationId
        minSdkVersion rootProject.ext.androids.minSdkVersion
        targetSdkVersion rootProject.ext.androids.targetSdkVersion
        versionCode rootProject.ext.androids.versionCode
        versionName rootProject.ext.androids.versionName
        manifestPlaceholders rootProject.ext.androids.manifestPlaceholders
        //语言环境和logcat日志TAG设置
        buildConfigField "String", "LOG_TAG", "\"${rootProject.ext.APP_LOG_TAG}\""
        //必须给出默认值，不然其他渠道包域名初始化为空
        buildConfigField "boolean", "IS_TEST", "false"
        buildConfigField "int", "HOST_NOW", "1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags "-frtti -fexceptions"
            }
        }
        //设置NDK的模块名称及平台
        ndk {
//            moduleName "native-lib"
            moduleName "zbar"
            abiFilters "armeabi", "armeabi-v7a", "x86"
        }
    }
    //为了解决部分第三方库重复打包了META-INF的问题
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    //签名
    signingConfigs {
        release {
            storeFile file("../chat_assistant.jks")
            storePassword "jusenrchat123"
            keyAlias "chatAssistant"
            keyPassword "jusenrchat123"
        }
    }

    //源码集
    sourceSets {
        main {
            //设置本地代码，[]内容去掉则禁止AS的NDK自动编译。
            jni.srcDirs = ['src/main/jni', 'src/main/cpp']
            jniLibs.srcDirs = ['libs']
            java.srcDirs = ['src/main/java']
            assets.srcDirs = ['src/main/assets']
        }
    }

    buildTypes {
        release {
            //显示Log状态
            buildConfigField "boolean", "LOG_DEBUG", "false"
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            //显示Log状态
            buildConfigField "boolean", "LOG_DEBUG", "true"
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    //产品渠道配置
    productFlavors {
        innerTest {
            buildConfigField "boolean", "IS_TEST", "true"
            buildConfigField "int", "HOST_NOW", "2"
            versionName = "T" + rootProject.getVersionName()
        }

        dev {
            buildConfigField "boolean", "IS_TEST", "true"
            buildConfigField "int", "HOST_NOW", "3"
            versionName = "D" + rootProject.getVersionName()
        }

        online {
            versionName = "V" + rootProject.getVersionName()
        }

        xiaomi { versionName = "V" + defaultConfig.versionName }
        huawei { versionName = "V" + defaultConfig.versionName }

        productFlavors.all { flavor ->
            flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
        }
    }

    //Apk包名自定义
    applicationVariants.all {
        variant ->
            def apkFileName = rootProject.getVersionName()
            variant.outputs.each {
                output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        def fileName = outputFile.name.replace(".apk", "-${apkFileName}.apk")
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
            }
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile project(':chat_library')

    testCompile rootProject.ext.dependencies.junit
    //查看App数据库使用(方法：1.放开注释 2.重新编译 3.使用'DebugDB'字段检索日志 4.访问检索到的连接，OK!)
    debugCompile rootProject.ext.dependencies.debug_db

    debugCompile rootProject.ext.dependencies.leakcanary_android
    testCompile rootProject.ext.dependencies.leakcanary_android_no_op
    releaseCompile rootProject.ext.dependencies.leakcanary_android_no_op

    compile rootProject.ext.dependencies.permission
    compile rootProject.ext.dependencies.core
    compile rootProject.ext.dependencies.ptlog

    annotationProcessor rootProject.ext.dependencies.butterknife_compiler
}

//编译结束后删除unaligned包
android.applicationVariants.all { variant ->
    variant.assemble.doLast {
        variant.outputs.each { output ->
            println "aligned " + output.outputFile
            println "unaligned " + output.packageApplication.outputFile

            File file = output.packageApplication.outputFile;
            if (variant.buildType.zipAlignEnabled && file.getName().contains("unaligned")) {
                println "deleting " + file.getName()
                file.delete()
            }
        }
    }
}